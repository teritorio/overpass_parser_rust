WHITESPACE = _{ " " | "\t" | "\n" | "\r" | "\u{0C}" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* ~ "\n" | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

INTEGER = @{ ASCII_DIGIT+ }
FLOAT = @{ "-"? ~ INTEGER ~ "." ~ INTEGER }
operator = { "=" | "~=" | "=~" | "!=" | "!~" | "~" }
NOT = { "!" }
UNQUOTED_STRING = @{ (ASCII_ALPHANUMERIC | "-" | "_")+ }

SIMPLE_QUOTED_STRING = @{ "'" ~ ( (!"'" ~ ANY) | ("\\'" ))* ~ "'" }
DOUBLE_QUOTED_STRING = @{ "\"" ~ ( (!"\"" ~ ANY) | ("\\\""))* ~ "\"" }
QUOTED_STRING = _{ SIMPLE_QUOTED_STRING | DOUBLE_QUOTED_STRING }

ID = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "-" | "_")* }
DOT_ID = _{ "." ~ ID }

STRING = { SIMPLE_QUOTED_STRING | DOUBLE_QUOTED_STRING | UNQUOTED_STRING | number }
osm_id = { INTEGER }
number = { FLOAT | INTEGER }

metadata = { "[" ~ "out:json" ~ "]" ~ ( "[" ~ "timeout:" ~ number ~ "]" )? }

selector = { "[" ~ not? ~ key ~ (operator ~ value)? ~ "]" }
not = { "!" }
key = @{ (STRING | "_" | ":" | "-")+ }
value = @{ (!"]" ~ ANY)+ }

filter_bbox = { number ~ "," ~ number ~ "," ~ number ~ "," ~ number }
filter_poly = { "poly:" ~ QUOTED_STRING }
filter_osm_id = { osm_id }
filter_osm_ids = { "id:" ~ osm_id ~ ("," ~ osm_id)* }
filter_area = { "area" ~ DOT_ID }
filter_around_core = { DOT_ID }
filter_around_radius = { number }
filter_around = { "around" ~ filter_around_core ~ ":" ~ filter_around_radius }
filter = { "(" ~ ( filter_bbox | filter_poly | filter_osm_id | filter_osm_ids | filter_area | filter_around ) ~ ")" }

asignation = { "->" ~ DOT_ID }

object_type = { "node" | "way" | "relation" | "area" | "nwr" }
query_object = { object_type ~ DOT_ID? ~ selector* ~ filter* ~ asignation? }
query_recurse = { ( "<" | "<<" | ">" | ">>" ) ~ asignation? }
query_union = { "(" ~ (query_sequence ~ ";" )+ ~ ")" ~ asignation? }
query_sequence = { query_object | query_recurse | query_union }

out_geom = { "geom" | "center" | "bb" }
out_level_of_details = { "ids" | "skel" | "body" | "tags" | "meta" }
out = { "out" ~ out_geom? ~ out_level_of_details? }

subrequest = { ( query_sequence ~ ";" )+ ~ (out ~ ";")? }
request = { (WHITESPACE | COMMENT)* ~ (metadata ~ ";" )? ~ subrequest+ }
